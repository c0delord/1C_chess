// Создает элемент справочника Фигуры с указанными занчениями
// Параметры:
//   Тип - ПеречислениеСсылка.ТипыФигур - тип новой фигуры
//   Цвет - ПеречислениеСсылка.Цвета - цвет новой фигуры
//   Ценность - Число - условная система очков для оценки силы фигуры
//   Вертикаль - Число - номер клетки по вертикали для поиска позиции по-умолчанию
//   Горизонталь - Число - номер клетки по горизонтали для поиска позиции по-умолчани
//   Доска - Таблица значений - набор клеток, в котором выполняется поиск позиции по-умолчани для новой фигуры
//		
Процедура СоздатьФигуру(Тип, Цвет, Ценность, Вертикаль, Горизонталь, Доска) Экспорт
	КлеткаПоУмолчанию = Неопределено;
	Клетки = Доска.НайтиСтроки(Новый Структура("Горизонталь,Вертикаль", Горизонталь, Вертикаль));
	Если Клетки.Количество() = 1 Тогда
		КлеткаПоУмолчанию = Клетки[0];
	Иначе
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Ошибка поиска клетки: %1%2'"), Горизонталь, Вертикаль);
        ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	НоваяФигура = Справочники.Фигуры.СоздатьЭлемент();
	НоваяФигура.Тип = Тип;
	НоваяФигура.Наименование = Строка(Тип);
	НоваяФигура.Цвет = Цвет;
	НоваяФигура.Ценность = Ценность;
	НоваяФигура.Символ = Механика.ПолучитьСимволФигуры(Тип, Цвет);
	НоваяФигура.КлеткаПоУмолчанию = КлеткаПоУмолчанию.Ссылка;
	Попытка
		НоваяФигура.Записать();	
	Исключение
		ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Сообщить(ИнформацияОбОшибке);	
	КонецПопытки;	
КонецПроцедуры

// Удаляет все элементы справочника
// Параметры:
//   НазваниеСправочника - Строка - имя справочника
//	
Процедура ОчиститьСправочник(НазваниеСправочника) Экспорт
	Выборка = Справочники[НазваниеСправочника].Выбрать();
	Пока Выборка.Следующий() Цикл
		Элем = Выборка.Ссылка.ПолучитьОбъект();
		Попытка	
			Элем.Удалить();	
		Исключение
			ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщить(ИнформацияОбОшибке);	
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

// Создает стандартный набор клеток для шахматной доски 8x8
//
Процедура СоздатьКлетки() Экспорт
	ЦветБелый = Перечисления.Цвета.Белый;
	ЦветЧерный = Перечисления.Цвета.Черный;
	
	Для Вертикаль = 1 По 8 Цикл
		Для Горизонталь = 1 По 8 Цикл
			НоваяКлетка = Справочники.Клетки.СоздатьЭлемент();
			НоваяКлетка.Вертикаль = Механика.ЧисловВСимвол(Вертикаль);
			НоваяКлетка.Горизонталь = Горизонталь;
			НоваяКлетка.Наименование = НоваяКлетка.Вертикаль + НоваяКлетка.Горизонталь;
			
			Если (Вертикаль % 2) = (Горизонталь % 2) Тогда
				НоваяКлетка.Цвет = ЦветЧерный; 
			Иначе
				НоваяКлетка.Цвет = ЦветБелый;
			КонецЕсли;
			НоваяКлетка.Записать();
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры