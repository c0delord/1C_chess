Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Если Не ЗначениеЗаполнено(Партия) Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Запрещено проводить документы Создания партии без указания партии!'");
			Сообщение.Сообщить();
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СозданиеПартииДоска.Фигура КАК Фигура,
		|	СозданиеПартииДоска.Клетка КАК Клетка
		|ИЗ
		|	Документ.СозданиеПартии.Доска КАК СозданиеПартииДоска
		|ГДЕ
		|	СозданиеПартииДоска.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.СостояниеПартии.Добавить();
		Движение.Период = Дата;
		Движение.Партия = Партия;
		Движение.Клетка = ВыборкаДетальныеЗаписи.Клетка;
		Движение.Фигура = ВыборкаДетальныеЗаписи.Фигура; 
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Фигура) Тогда
			Движение = Движения.СостояниеФигур.Добавить();
			Движение.Взята	= Ложь;
			Движение.Период = Дата;
			Движение.Партия = Партия;
			Движение.Фигура = ВыборкаДетальныеЗаписи.Фигура;
		КонецЕсли;	
	КонецЦикла;

	Движения.СостояниеПартии.Записывать = Истина;
	Движения.СостояниеФигур.Записывать = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(СостояниеПартииСрезПоследних.Регистратор) КАК РегистраторПредставление
		|ИЗ
		|	РегистрСведений.СостояниеПартии.СрезПоследних(&МоментВремени, Партия = &Партия) КАК СостояниеПартииСрезПоследних";
	
	ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Исключая);
	Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
	Запрос.УстановитьПараметр("Партия", Партия);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'По указанной партии уже есть движения: %1'"), 
								ВыборкаДетальныеЗаписи.РегистраторПредставление);
		Сообщение.Сообщить();
	КонецЕсли;

КонецПроцедуры
